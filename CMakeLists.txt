cmake_minimum_required(VERSION 3.31)
project(raygui C)

include(FetchContent)
FetchContent_Declare(discover_sources GIT_REPOSITORY https://github.com/CesarBerriot/discover_sources.git GIT_TAG 1.0.0)
FetchContent_Declare(raylib GIT_REPOSITORY https://github.com/raysan5/raylib.git GIT_TAG 5.5)
FetchContent_Declare(raygui_source GIT_REPOSITORY https://github.com/raysan5/raygui.git GIT_TAG 4.0)
FetchContent_MakeAvailable(discover_sources raylib raygui_source)
include(${discover_sources_SOURCE_DIR}/discover_sources.cmake)

block()
  file(GLOB glob LIST_DIRECTORIES TRUE ${raygui_source_SOURCE_DIR}/styles/*)
  foreach(entry IN LISTS glob)
    if(IS_DIRECTORY ${entry} AND NOT entry MATCHES "default$")
      block()
        file(GLOB glob ${entry}/*.h)
        list(GET glob 0 header)
        file(COPY ${header} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/include/raygui/styles)
      endblock()
    endif()
  endforeach()
endblock()

add_library(raygui source/implementation.c)
target_include_directories(raygui PUBLIC ${raygui_source_SOURCE_DIR}/src ${CMAKE_CURRENT_BINARY_DIR}/include)
target_link_libraries(raygui PUBLIC raylib)
if(MSVC)
  target_compile_options(raygui PRIVATE /W0)
endif()